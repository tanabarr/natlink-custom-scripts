# keyboard mappings for "screen" unix tool
# screen.vch

screen new = {Ctrl+a}"c";
screen remove = {Ctrl+a}"X";
screen crop = {Ctrl+a}"Q";
scroll back = {Ctrl+a}"["{pgup}{pgup};
scroll mode = {Ctrl+a}"[";
screen paste = {Ctrl+a}"]";
screen mark = '>';
screen kill = {Ctrl+a}"k";
screen help = {Ctrl+a}"?";
screen split [0..9] = 
    {Ctrl+a}"S"{ctrl+a}{tab}{ctrl+a}"n"
    When($1, Wait(200) {ctrl+a}{$1});
screen title = {Ctrl+a}"A";
screen list = {Ctrl+a}'"';
screen switch [0..9] = 
    When($1, 
         Repeat($1, {Ctrl+a}{tab} Wait(200)), 
         {ctrl+a}{tab});
screen detach = {Ctrl+a}"d";
screen next = {Ctrl + a} {n};
screen back = {Ctrl + a} {p};
screen 0..9 = {Ctrl + a} {$1};
#screen detach = {Ctrl + T} {d};
screen attach = "screen  {Enter}  {enter}";
screen join = "screen -x {enter}";
screen search (backward=? | forward=/)= {ctrl+a}"[$1";
screen search next (backward=? | forward=/)= "$1"{enter};

_rename(title) := {ctrl+a}"A" Repeat(3, {backspace}) $title {enter} {ctrl+a}"n";
screen set up chrome = 
 Repeat(9, {ctrl+a}"c") 
 {ctrl+a}0
 _rename("manager")
 _rename("git")
 _rename("debug")
 _rename("fab")
 _rename("vm5")
 _rename("vm6")
 _rename("job log")
 _rename("test")
 _rename("test log")
 _rename("debug2");
